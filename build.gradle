plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.17.0'
}

group 'jp.kitabatakep'
version '2.0.0'  // 插件版本号 - 发布新版本时更新此行

sourceCompatibility = 17

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version.set('2024.3')  // 编译时依赖的IntelliJ版本 - 使用最低支持版本确保向后兼容
    type.set('IC')         // 使用Community版本进行编译，确保更好的兼容性
    plugins.set([])        // 明确声明不依赖其他插件
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

//runIde {
//    ideDirectory '/Applications/RubyMine.app/Contents'
//}

// patch of https://youtrack.jetbrains.com/issue/KTIJ-782
tasks.buildSearchableOptions {
    enabled = false
}

patchPluginXml {
    def stripTag = { text, tag -> text.replace("<${tag}>", "").replace("</${tag}>", "") }
    def bodyInnerHTML = { path ->
        stripTag(stripTag(file(path).text, "html"), "body")
    }

    // 设置版本兼容范围 - 向前兼容（最低2024.3，兼容到2025.3）
    sinceBuild.set('243')    // 最低支持版本：2024.3 (构建号243)
    untilBuild.set('253.*')  // 最高支持版本：2025.3.x (需要时可手动更新到更高版本)
    
    pluginDescription.set(provider{ bodyInnerHTML("src/main/resources/META-INF/description.html") })
    changeNotes.set(provider{ bodyInnerHTML("src/main/resources/META-INF/changeNotes.html") } )
}

// 插件验证配置 - 验证向前兼容性
runPluginVerifier {
    // 测试版本：覆盖支持的版本范围
    ideVersions.set(['2024.3', '2025.1', '2025.2'])
    // 设置失败级别，只关注严重的兼容性问题
    failureLevel.set(['COMPATIBILITY_PROBLEMS'])
    // 下载目录配置
    downloadDir.set(file("${project.buildDir}/pluginVerifier/ides"))
}