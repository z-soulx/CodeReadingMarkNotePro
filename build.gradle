plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.17.0'
}

group 'jp.kitabatakep'
version '2024.3.0'  // 插件版本号 - 发布新版本时更新此行

sourceCompatibility = 17

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version.set('2024.3')  // 编译时依赖的IntelliJ版本 - 使用最低支持版本确保向后兼容
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

//runIde {
//    ideDirectory '/Applications/RubyMine.app/Contents'
//}

// patch of https://youtrack.jetbrains.com/issue/KTIJ-782
tasks.buildSearchableOptions {
    enabled = false
}

patchPluginXml {
    def stripTag = { text, tag -> text.replace("<${tag}>", "").replace("</${tag}>", "") }
    def bodyInnerHTML = { path ->
        stripTag(stripTag(file(path).text, "html"), "body")
    }

    // 设置版本兼容范围 - 向前兼容（最低2024.3，兼容2025版本）
    sinceBuild.set('243')    // 最低支持版本：2024.3 (构建号243)
    untilBuild.set('')       // 最高支持版本：无限制，支持2025.1/2025.2等所有未来版本
    
    pluginDescription.set(provider{ bodyInnerHTML("src/main/resources/META-INF/description.html") })
    changeNotes.set(provider{ bodyInnerHTML("src/main/resources/META-INF/changeNotes.html") } )
}

// 插件验证配置 - 验证向前兼容性
runPluginVerifier {
    // 测试版本：当前支持的最低版本 + 最新开发版本（包含2025.x）
    ideVersions.set(['2024.3', 'LATEST-EAP-SNAPSHOT'])
    // 设置失败级别，只关注严重的兼容性问题
    failureLevel.set([RunPluginVerifierTask.FailureLevel.COMPATIBILITY_PROBLEMS])
}